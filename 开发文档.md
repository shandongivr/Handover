# 开发文档
> 我自定义了一个头文件(sha.flw)放在`C:/Windows/`目录下，开发新流程时，如果用到我定义的函数，需要先导入这个文件，即`#include "sha.flw"`  
---

### sha.flw文件内容  
```shine  
bool func_playvox(string voxpath)//放音
{
	setvoiceformat(_vfOki32);
		playfile(voxpath,"?",1);
		success return 1;
		dtmfdetected return 1;
		stopped return 0;
		fail return 0;
	}
	
bool func_playvox_nobtn(string voxpath)//放音不能按键打断
{
	setvoiceformat(_vfOki32);
		playfile(voxpath,"",1);
		success return 1;
		stopped return 0;
		fail return 0;
	}
	
bool func_iswork(string am,string pm)//工作日
{
	string time;
	int week;
	stMain:
		time=getnowtime("%H:%M");
		week=gettimeinfo(getnowtime(),_dtWeekday);
		if(week==6||week==7)
			return 0;
		else
			->stJudgetime
	stJudgetime:
		if(time>=parseparam(am,1,"-")&&time<=parseparam(am,2,"-")||time>=parseparam(pm,1,"-")&&time<=parseparam(pm,2,"-"))
			return 1;
		else return 0;
	
	}
	
bool func_isweek()//周末
{
	int week;
	stMain:
		week=gettimeinfo(getnowtime(),_dtWeekday);
		if(week==6||week==7)
			return 1;
		else return 0;
	}
	
bool func_onceCall(string number)//转呼一个号码
{
stMakeCall:	
	MakeCall(number,_Caller,"");
	//playfile("../makecall/wait","",1);
	makecall.Success->stTimer
	makecall.Alerting->stWaitasec
	makecall.Fail->stStopWait	
	makecall.callcleared->stStopWait
stWaitasec:
	starttimer(1000);
	timeout->stUnlink
	makecall.fail->stStopWait
	makecall.success->stRelink
	makecall.callcleared->stStopWait
stUnlink:
	unlinkchannel(_OutChannel);
	success->stLinkVoice
	fail->stStopWait
	makecall.fail->stStopWait
	makecall.success->stRelink
	makecall.callcleared->stStopWait
stLinkVoice:
	linkchannel(_InChannel,_VoiceChannel,_ldBiDirect);
	success->stPlayToUser
	fail->stRelink
	makecall.fail->stStopWait
	makecall.success->stRelink
	makecall.callcleared->stStopWait
	
stPlayToUser:	
	SetVoiceFormat(_vfOki32);
	PlayFile("../makecall/wait","?",1);
	success->stPlayToUser
	DtmfDetected->stPlayToUser
	stopped->stRelink
	fail->stRelink
	makecall.success->stStopMedia
	makecall.fail->stStopWait
	makecall.callcleared->stStopWait

stStopMedia:
	StopMedia();
	success->stRelink
	fail->stRelink
	makecall.fail->stStopWait
	makecall.callcleared->stStopWait
stRelink:
	linkchannel(_OutChannel,_InChannel,_ldBiDirect); 
	success->stTimer
	fail->stStopWait
	makecall.fail->stStopWait
	makecall.callcleared->stStopWait

stTimer:
	StartTimer(36000000);
	timeout->stTimer
	makecall.Success->stTimer
	makecall.CallCleared->stStopWait
	
stStopWait:
	StopMedia();
	success return 0;
	fail return 0;
	}
	
bool func_orderCall(string tels)
{
	int i =1;
	string number;
	stMain:
		number=parseparam(tels,i,"#");
		if(number=="")
			->stAnswer
		else->stMakeCall
	stMakeCall:
		if(func_onceCall(number))
			return 1;
		else
			{
				i=i+1;
				->stMain
				}
	stAnswer:
		setvoiceformat(_vfOki32);
		playfile("../makecall/busy","?",1);  //对不起,线路正忙,请稍候再拨
 		Success return 0;
 		dtmfdetected return 0;
 		stopped return 0;
 		Fail return 0;
	}
	
int self_getCount(string ss,string sub)
{
int i=0;
string s;
stGetNum:
 i=i+1;
 s=parseparam(ss,i,sub);
 if(s=="")
   return (i-1);
 else
   ->stGetNum
	}	

bool func_RandCall(string tels)
{
	int count,i;
	string number;
	stMain:
		count=self_getCount(tels,"#");
		->stGetNum
	stGetNum:
		if(tels=="")
			return 0;
		else
			{
			i=rand(1,count);
			number=ParseParam(tels,i,"#");
			IR0=strstr(tels,number);
			SR1=strleft(tels,IR0-1);
			IR1=IR0+strlen(number)+1;
			IR2=strlen(tels);
			SR2=substr(tels,IR1,IR2);
			tels=trim(SR1+SR2,"#");
			count=count-1;
			->stRandCall
		}
	stRandCall:
		if(func_onceCall(number))
			return 1;
		else
			->stCancelCall
	stCancelCall:
		CancelMakeCall();
		success->stGetNum
		fail->stGetNum
	}

int	IntFuncDigit(string sPlayFile,string sDtmf,string sReturn)	
{
//	local variables 
stWait:	
	->	stPrompt
stPrompt:	
	setvoiceformat(_vfOki32);
	playfile( sPlayFile, sDtmf, 1);	
		success		-> stHaveRet
		dtmfdetected	-> stInput
		fail		-> stExit
stHaveRet:
	if(sReturn=="")
		->stInput
	else
		->stIfReturn
stIfReturn:	
	playfile( sReturn, sDtmf, 1);	
		success		-> stInput
		dtmfdetected	-> stInput
		fail		-> stInput
stInput:	
	getdtmf(1, 1, "?", 0);	
		dtmfdetected	-> stDigitOK
		DtmfDetected['#'] -> stDigitJ
		DtmfDetected['*'] -> stDigitX
		timeout	-> stPrompt
		fail	-> stExit
stDigitOK:	
	IR0 = _DTMFString;
	->	stExit
stDigitJ:	
	IR0 = 11;	
	->	stExit
stDigitX:	
	IR0 = 12;	
	->	stExit
stExit:	
	return	IR0;	
}
```
